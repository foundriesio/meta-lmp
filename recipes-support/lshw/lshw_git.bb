DESCRIPTION = "A small tool to provide detailed information on the hardware \
configuration of the machine. It can report exact memory configuration, \
firmware version, mainboard configuration, CPU version and speed, cache \
configuration, bus speed, etc. on DMI-capable or EFI systems."
SUMMARY = "Hardware lister"
HOMEPAGE = "http://ezix.org/project/wiki/HardwareLiSter"
SECTION = "console/tools"
LICENSE = "GPLv2+"
LIC_FILES_CHKSUM = "file://COPYING;md5=b234ee4d69f5fce4486a80fdaf4a4263"

DEPENDS = "gettext-native pciutils usbutils"
COMPATIBLE_HOST = "(i.86|x86_64|arm|aarch64|riscv64).*-linux"

SRC_URI = "git://github.com/lyonel/lshw.git \
    file://cross-compile.patch \
    file://ldflags.patch \
    file://docbook2man.patch \
"

SRC_URI_append_libc-musl = " \
    file://0001-sysfs-Fix-basename-build-with-musl.patch \
    file://0002-Fix-use-of-LONG_BIT.patch \
"

SRCREV = "20cda77239e8604e798b87a0441e694edb0214d1"

S = "${WORKDIR}/git"

do_compile() {
    # build core only - don't ship gui
    oe_runmake -C src core
}

do_install() {
    oe_runmake install DESTDIR=${D}
    # data files provided by dependencies
    rm -rf ${D}/usr/share/lshw
}
