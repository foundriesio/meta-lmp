# LMP specific configuration

# Beaglebone
PREFERRED_PROVIDER_virtual/bootloader:beaglebone-yocto = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:beaglebone-yocto = "u-boot-fio"
WKS_FILE_DEPENDS:append:beaglebone-yocto = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:beaglebone-yocto = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES:append:beaglebone-yocto = " ubootenv"
OSTREE_KERNEL_ARGS:beaglebone-yocto ?= "console=ttyS0,115200n8 ${OSTREE_KERNEL_ARGS_COMMON}"
KERNEL_DEVICETREE:append:beaglebone-yocto = " am335x-boneblack-wireless.dtb"
IMAGE_BOOT_FILES:beaglebone-yocto = "u-boot.img MLO boot.itb"
KERNEL_IMAGETYPE:beaglebone-yocto = "fitImage"
KERNEL_CLASSES:beaglebone-yocto = " kernel-lmp-fitimage "
## beaglebone-yocto.conf appends kernel-image-zimage by default
IMAGE_INSTALL:remove:beaglebone-yocto = "kernel-image-zimage"

# Raspberry Pi
KERNEL_BUILTIN_WIREGUARD:rpi = "1"
PREFERRED_PROVIDER_virtual/kernel:rpi ?= "linux-lmp-rpi"
PREFERRED_PROVIDER_virtual/bootloader:rpi = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:rpi = "u-boot-fio"
SOTA_CLIENT_FEATURES:append:rpi = " ubootenv"
WKS_FILE_DEPENDS_BOOTLOADERS:rpi = "virtual/bootloader"
WKS_FILE_DEPENDS:append:rpi = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:rpi = "u-boot-ostree-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:rpi = "u-boot-ostree-scr-fit"
PREFERRED_PROVIDER_virtual/dtb:rpi ?= "lmp-device-tree"
IMAGE_FSTYPES:remove:rpi = "ext3 rpi-sdimg"
IMAGE_BOOT_FILES:rpi = "${BOOTFILES_DIR_NAME}/* u-boot.bin;${SDIMG_KERNELIMAGE} ${KERNEL_IMAGETYPE} ${@make_dtb_boot_files(d)} boot.scr uEnv.txt"
IMAGE_BOOT_FILES:sota:rpi = "${BOOTFILES_DIR_NAME}/* u-boot.bin;${SDIMG_KERNELIMAGE} ${@make_dtb_boot_files(d)} boot.itb"
KERNEL_CLASSES:sota:rpi = " kernel-lmp-fitimage "
KERNEL_IMAGETYPE:sota:rpi = "fitImage"
## Workaround for https://github.com/raspberrypi/firmware/issues/1483
ENABLE_UART:rpi = "1"
## Mimic meta-raspberrypi behavior
KERNEL_SERIAL:rpi ?= "${@oe.utils.conditional("ENABLE_UART", "1", "console=ttyS0,115200", "", d)}"
KERNEL_SERIAL:raspberrypi-cm3 ?= "console=ttyAMA0,115200"
OSTREE_KERNEL_ARGS_COMMON_RPI ?= "coherent_pool=1M 8250.nr_uarts=1 console=tty1 cgroup_enable=memory ${KERNEL_SERIAL} ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS:raspberrypi3 ?= "vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 ${OSTREE_KERNEL_ARGS_COMMON_RPI}"
OSTREE_KERNEL_ARGS:raspberrypi-cm3 ?= "vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 ${OSTREE_KERNEL_ARGS_COMMON_RPI}"
OSTREE_KERNEL_ARGS:raspberrypi4 ?= "vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 ${OSTREE_KERNEL_ARGS_COMMON_RPI}"
## U-Boot entrypoints for rpi
UBOOT_ENTRYPOINT:rpi = "0x00200000"
UBOOT_DTB_LOADADDRESS:rpi = "0x02600000"
UBOOT_DTBO_LOADADDRESS:rpi = "0x026d0000"
## Rpi 4/CM4
MACHINE_EXTRA_RRECOMMENDS:append:raspberrypi4 = " linux-firmware-rpidistro-bcm43456"

# RISC-V targets
PREFERRED_PROVIDER_virtual/bootloader:qemuriscv64 = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:qemuriscv64 = "u-boot-fio"
IMAGE_BOOT_FILES:qemuriscv64 = "boot.itb"
KERNEL_IMAGETYPE:qemuriscv64 = "fitImage"
WKS_FILE_DEPENDS:append:qemuriscv64 = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:qemuriscv64 = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES:append:qemuriscv64 = " ubootenv"
KERNEL_IMAGETYPES:remove:qemuriscv64 = "uImage"
KERNEL_CLASSES:qemuriscv64 = " kernel-lmp-fitimage "
OSTREE_KERNEL_ARGS:qemuriscv64 ?= "earlycon=sbi ${OSTREE_KERNEL_ARGS_COMMON}"
UBOOT_ENTRYPOINT:qemuriscv64 = "0x80400000"
RISCV_SBI_PAYLOAD:qemuriscv64 = "u-boot.bin"
QB_DEFAULT_BIOS:qemuriscv64 = "fw_payload.elf"
QB_DRIVE_TYPE:qemuriscv64 = "/dev/vdb"
QB_OPT_APPEND:append:qemuriscv64 = " -bios ${DEPLOY_DIR_IMAGE}/fw_payload.elf"

# QEMU ARM
PREFERRED_PROVIDER_virtual/bootloader:qemuarm64 = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:qemuarm64 = "u-boot-fio"
WKS_FILE_DEPENDS:append:qemuarm64 = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:qemuarm64 = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES:append:qemuarm64 = " ubootenv"
IMAGE_BOOT_FILES:qemuarm64 = "boot.itb"
KERNEL_IMAGETYPE:qemuarm64 = "fitImage"
KERNEL_CLASSES:qemuarm64 = " kernel-lmp-fitimage "
OSTREE_KERNEL_ARGS:qemuarm64 ?= "console=ttyAMA0 ${OSTREE_KERNEL_ARGS_COMMON}"
UBOOT_ENTRYPOINT:qemuarm64 = "0x40080000"
MACHINE_FEATURES:append:qemuarm64-secureboot = " optee"
OSTREE_SPLIT_BOOT:qemuarm64-secureboot = "1"
WKS_FILE:sota:qemuarm64-secureboot = "sdimage-split-boot-sota.wks.in"
## Use same minimal memory amount as suggested by op-tee
QB_MEM:qemuarm64-secureboot = "-m 1057"
QB_DRIVE_TYPE:qemuarm64-secureboot = "/dev/vdb"
## EBBR / SystemReady
KERNEL_IMAGETYPE:qemuarm64-secureboot-ebbr ?= "Image"
KERNEL_CLASSES:qemuarm64-secureboot-ebbr ?= ""
EFI_PROVIDER:qemuarm64-secureboot-ebbr ?= "systemd-boot"
OSTREE_SPLIT_BOOT:qemuarm64-secureboot-ebbr = "1"
OSTREE_LOADER_LINK:qemuarm64-secureboot-ebbr = "0"
OSTREE_BOOTLOADER:qemuarm64-secureboot-ebbr ?= "none"
IMAGE_BOOT_FILES:qemuarm64-secureboot-ebbr = ""
WKS_FILE:qemuarm64-secureboot-ebbr:sota ?= "efidisk-sota.wks.in"

# QEMU ARM
PREFERRED_PROVIDER_virtual/bootloader:qemuarm = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:qemuarm = "u-boot-fio"
WKS_FILE_DEPENDS:append:qemuarm = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:qemuarm = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES:append:qemuarm = " ubootenv"
IMAGE_BOOT_FILES:qemuarm = "boot.itb"
KERNEL_IMAGETYPE:qemuarm = "fitImage"
KERNEL_CLASSES:qemuarm = " kernel-lmp-fitimage "
OSTREE_KERNEL_ARGS:qemuarm ?= "console=ttyAMA0 ${OSTREE_KERNEL_ARGS_COMMON}"
UBOOT_ENTRYPOINT:qemuarm = "0x40080000"
QB_DRIVE_TYPE:qemuarm = "/dev/vdb"
## Currently support u-boot -> Linux boot
## will add more security later
QB_OPT_APPEND:qemuarm = "-no-acpi -bios ${DEPLOY_DIR_IMAGE}/u-boot.bin -d unimp"

# ARM64 Generic (SystemReady)
OSTREE_BOOTLOADER:generic-arm64 ?= "none"
MACHINE_FEATURES:append:generic-arm64 = " acpi pci usbhost"
EFI_PROVIDER:generic-arm64 ?= "systemd-boot"
OSTREE_SPLIT_BOOT:generic-arm64 = "1"
OSTREE_LOADER_LINK:generic-arm64 = "0"
WKS_FILE:generic-arm64:sota ?= "image-efi-installer.wks.in"
WKS_FILE_DEPENDS:append:generic-arm64 = " ${INITRD_IMAGE_LIVE}"
## wic-based installer requires image to be available via IMAGE_BOOT_FILES
IMAGE_BOOT_FILES:generic-arm64 = "${@bb.utils.contains('WKS_FILE', 'image-efi-installer.wks.in', 'systemd-bootaa64.efi;EFI/BOOT/bootaa64.efi systemd-bootaa64.efi;EFI/systemd/systemd-bootaa64.efi ${IMGDEPLOYDIR}/${IMAGE_BASENAME}-${MACHINE}.ota-ext4;rootfs.img', '', d)}"

# Intel
OSTREE_BOOTLOADER:intel-corei7-64 ?= "none"
OSTREE_KERNEL_ARGS:intel-corei7-64 ?= "console=ttyS0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
EFI_PROVIDER:intel-corei7-64 = "systemd-boot"
OSTREE_SPLIT_BOOT:intel-corei7-64 = "1"
OSTREE_LOADER_LINK:intel-corei7-64 = "0"
KERNEL_CLASSES:intel-corei7-64 = " kernel-lmp-efi "
WKS_FILE:intel-corei7-64:sota ?= "efidisk-sota.wks.in"
WKS_FILE_DEPENDS:append:intel-corei7-64 = " ${INITRD_IMAGE_LIVE}"
## wic-based installer requires image to be available via IMAGE_BOOT_FILES
IMAGE_BOOT_FILES:intel-corei7-64 = "${@bb.utils.contains('WKS_FILE', 'image-efi-installer.wks.in', 'systemd-bootx64.efi;EFI/BOOT/bootx64.efi systemd-bootx64.efi;EFI/systemd/systemd-bootx64.efi startup.nsh ${IMGDEPLOYDIR}/${IMAGE_BASENAME}-${MACHINE}.ota-ext4;rootfs.img', '', d)}"

# Common for iMX targets
## Prefer IMX_DEFAULT_BSP=nxp as mainline removes every common machine override
IMX_DEFAULT_BSP = "nxp"
OSTREE_KERNEL_ARGS:mx6-generic-bsp ?= "console=tty1 console=ttymxc0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS:mx6ull-generic-bsp ?= "console=tty1 console=ttymxc0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS:mx7d-generic-bsp ?= "console=tty1 console=ttymxc0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS:mx7ulp-generic-bsp ?= "console=tty1 console=ttyLP0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"

# Embedded Artists i.MX7ULP COM
UBOOT_SIGN_ENABLE:sota:imx7ulpea-ucom = "1"
PREFERRED_PROVIDER_u-boot-default-script:imx7ulpea-ucom = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:imx7ulpea-ucom = "u-boot-ostree-scr-fit"
LMP_BOOT_FIRMWARE:imx7ulpea-ucom = "SPL u-boot.itb"
IMAGE_BOOT_FILES:imx7ulpea-ucom = "boot.scr uEnv.txt zImage ${@make_dtb_boot_files(d)}"
IMAGE_BOOT_FILES:sota:imx7ulpea-ucom = "boot.itb"
KERNEL_IMAGETYPE:sota:imx7ulpea-ucom = "fitImage"
KERNEL_CLASSES:sota:imx7ulpea-ucom = " kernel-lmp-fitimage "
OSTREE_KERNEL_ARGS_COMMON:imx7ulpea-ucom ?= "earlycon root=/dev/mmcblk0p2 rootfstype=ext4"
SOTA_CLIENT_FEATURES:append:imx7ulpea-ucom = " ubootenv"
UBOOT_CLASSES:imx7ulpea-ucom = "uboot-fitimage"
BOOTSCR_LOAD_ADDR:imx7ulpea-ucom = "0x60800000"
UBOOT_MACHINE:imx7ulpea-ucom = "mx7ulp_com_defconfig"
UBOOT_CONFIG:imx7ulpea-ucom = ""
KERNEL_BUILTIN_WIREGUARD:imx7ulpea-ucom = "1"

# iMX6UL
UBOOT_SIGN_ENABLE:sota:mx6ul-generic-bsp = "1"
PREFERRED_PROVIDER_virtual/kernel:mx6ul-nxp-bsp ?= "linux-lmp-fslc-imx"
IMX_DEFAULT_BOOTLOADER:mx6ul-generic-bsp ?= "u-boot-fio"
WKS_FILE_DEPENDS:append:mx6ul-generic-bsp = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:mx6ul-generic-bsp = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:mx6ul-generic-bsp = "u-boot-ostree-scr-fit"
SPL_BINARY:mx6ul-generic-bsp = "SPL"
UBOOT_CLASSES:mx6ul-generic-bsp = "uboot-fitimage"
UBOOT_CONFIG:mx6ul-generic-bsp = ""
UBOOT_SUFFIX:mx6ul-generic-bsp = "bin"
UBOOT_MAKE_TARGET:mx6ul-generic-bsp = ""
UBOOT_DTB_LOADADDRESS:mx6ul-generic-bsp = "0x85800000"
UBOOT_RD_LOADADDRESS:mx6ul-generic-bsp = "0x84000000"
MACHINE_FEATURES:append:mx6ul-generic-bsp = " optee"
LMP_BOOT_FIRMWARE_FILES:mx6ul-generic-bsp = "SPL u-boot.itb"
IMAGE_BOOT_FILES:sota:mx6ul-generic-bsp = " boot.itb"
IMAGE_BOOT_FILES:mx6ul-generic-bsp = " boot.scr uEnv.txt zImage"
KERNEL_IMAGETYPE:sota:mx6ul-generic-bsp = "fitImage"
KERNEL_CLASSES:sota:mx6ul-generic-bsp = " kernel-lmp-fitimage "
WKS_FILE:mx6ul-generic-bsp:sota = "sdimage-imx6-spl-fit-sota.wks"
# iMX6UL EVK
SOTA_CLIENT_FEATURES:append:imx6ulevk = " ubootenv"
UBOOT_MACHINE:imx6ulevk = "mx6ul_14x14_evk_defconfig"
KERNEL_DEVICETREE:imx6ulevk = "imx6ul-14x14-evk.dtb"
BOOTSCR_LOAD_ADDR:imx6ulevk = "0x85000000"

# iMX6ULL
UBOOT_SIGN_ENABLE:sota:mx6ull-generic-bsp = "1"
PREFERRED_PROVIDER_virtual/kernel:mx6ull-nxp-bsp ?= "linux-lmp-fslc-imx"
IMX_DEFAULT_BOOTLOADER:mx6ull-generic-bsp ?= "u-boot-fio"
WKS_FILE_DEPENDS:append:mx6ull-generic-bsp = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:mx6ull-generic-bsp = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:mx6ull-generic-bsp = "u-boot-ostree-scr-fit"
SPL_BINARY:mx6ull-generic-bsp = "SPL"
UBOOT_CLASSES:mx6ull-generic-bsp = "uboot-fitimage"
UBOOT_CONFIG:mx6ull-generic-bsp = ""
UBOOT_SUFFIX:mx6ull-generic-bsp = "bin"
UBOOT_MAKE_TARGET:mx6ull-generic-bsp = ""
UBOOT_DTB_LOADADDRESS:mx6ull-generic-bsp = "0x85800000"
UBOOT_RD_LOADADDRESS:mx6ull-generic-bsp = "0x84000000"
MACHINE_FEATURES:append:mx6ull-generic-bsp = " optee"
LMP_BOOT_FIRMWARE_FILES:mx6ull-generic-bsp = "SPL u-boot.itb"
IMAGE_BOOT_FILES:sota:mx6ull-generic-bsp = " boot.itb"
IMAGE_BOOT_FILES:mx6ull-generic-bsp = " boot.scr uEnv.txt zImage ${@make_dtb_boot_files(d)}"
KERNEL_IMAGETYPE:sota:mx6ull-generic-bsp = "fitImage"
KERNEL_CLASSES:sota:mx6ull-generic-bsp = " kernel-lmp-fitimage "
WKS_FILE:mx6ull-generic-bsp:sota = "sdimage-imx6-spl-fit-sota.wks"
# iMX6ULL EVK
SOTA_CLIENT_FEATURES:append:imx6ullevk = " ubootenv"
UBOOT_MACHINE:imx6ullevk = "mx6ull_14x14_evk_defconfig"
KERNEL_DEVICETREE:imx6ullevk = "imx6ull-14x14-evk.dtb"
BOOTSCR_LOAD_ADDR:imx6ullevk = "0x85000000"

# Toradex Apalis iMX6
UBOOT_SIGN_ENABLE:sota:apalis-imx6 = "1"
PREFERRED_PROVIDER_u-boot-default-script:apalis-imx6 = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:apalis-imx6 = "u-boot-ostree-scr-fit"
LMP_BOOT_FIRMWARE_FILES:apalis-imx6 = "SPL u-boot.itb"
IMAGE_BOOT_FILES:apalis-imx6 = "boot.scr uEnv.txt SPL u-boot.itb ${KERNEL_IMAGETYPE} ${@make_dtb_boot_files(d)}"
IMAGE_BOOT_FILES:remove:apalis-imx6 = "boot.scr-${MACHINE};boot.scr"
IMAGE_BOOT_FILES:sota:apalis-imx6 = "boot.itb"
KERNEL_IMAGETYPE:sota:apalis-imx6 = "fitImage"
KERNEL_CLASSES:sota:apalis-imx6 = " kernel-lmp-fitimage "
OSTREE_KERNEL_ARGS:apalis-imx6 ?= "console=tty1 console=ttymxc0,115200 root=/dev/mmcblk2p2 rootfstype=ext4"
PREFERRED_PROVIDER_virtual/kernel:apalis-imx6 ?= "linux-lmp"
KBUILD_DEFCONFIG:apalis-imx6 = ""
KERNEL_DEVICETREE:remove:apalis-imx6 = "imx6q-apalis-ixora-v1.2.dtb"
IMX_DEFAULT_BOOTLOADER:apalis-imx6 ?= "u-boot-fio"
SOTA_CLIENT_FEATURES:append:apalis-imx6 = " ubootenv"
UBOOT_CONFIG:apalis-imx6 = ""
UBOOT_SUFFIX:apalis-imx6 = "bin"
UBOOT_MACHINE:apalis-imx6 = "apalis_imx6_defconfig"
UBOOT_CLASSES:apalis-imx6 = "uboot-fitimage"
UBOOT_DTB_LOADADDRESS:apalis-imx6 = "0x15800000"
UBOOT_RD_LOADADDRESS:apalis-imx6 = "0x14000000"
WKS_FILE:apalis-imx6 = "sdimage-imx6dq-spl-fit.wks"
WKS_FILES:sota:apalis-imx6 = "sdimage-imx6dq-spl-fit-sota.wks"
MACHINE_FEATURES:append:apalis-imx6 = " optee"
BOOTSCR_LOAD_ADDR:apalis-imx6 = "0x15000000"

# Toradex Apalis iMX6 with secure boot support
SOTA_CLIENT_FEATURES:remove:sota:apalis-imx6-sec = "ubootenv"

# iMX8QM
SOTA_CLIENT_FEATURES:append:mx8qm-generic-bsp = " ubootenv"
IMAGE_BOOT_FILES:mx8qm-nxp-bsp = "imx-boot u-boot.itb hdmitxfw.bin ${KERNEL_IMAGETYPE} ${@make_dtb_boot_files(d)} boot.scr uEnv.txt"
IMAGE_BOOT_FILES:sota:mx8qm-nxp-bsp = "imx-boot u-boot.itb"
LMP_BOOT_FIRMWARE_FILES:mx8qm-nxp-bsp = "imx-boot u-boot.itb"
BOOTSCR_LOAD_ADDR:mx8qm-generic-bsp = "0x83100000"
KERNEL_IMAGETYPE:sota:mx8qm-generic-bsp = "fitImage"
KERNEL_CLASSES:sota:mx8qm-generic-bsp = " kernel-lmp-fitimage "
FIT_LOADABLES:mx8qm-generic-bsp ?= "hdmitxfw.bin"
WKS_FILE:mx8qm-nxp-bsp:sota = "sdimage-imx8-sota.wks.in"
IMX_DEFAULT_BOOTLOADER:mx8qm-generic-bsp ?= "u-boot-fio"
WKS_FILE_DEPENDS:append:mx8qm-generic-bsp = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:mx8qm-generic-bsp = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:mx8qm-generic-bsp = "u-boot-ostree-scr-fit"
UBOOT_CLASSES:mx8qm-generic-bsp = "uboot-fitimage"
IMXBOOT_TARGETS:mx8qm-nxp-bsp = "flash_evk_spl"
ATF_BINARY:mx8qm-generic-bsp = "arm-trusted-firmware.bin"
EXTRA_IMAGEDEPENDS:append:mx8qm-generic-bsp = "virtual/trusted-firmware-a"
UBOOT_ENTRYPOINT:mx8qm-generic-bsp = "0x80280000"
UBOOT_LOADADDRESS:mx8qm-generic-bsp = "0x80280000"
UBOOT_DTB_LOADADDRESS:mx8qm-generic-bsp = "0x83000000"
UBOOT_SIGN_ENABLE:sota:mx8qm-generic-bsp = "1"
## iMX8QM MEK
OSTREE_KERNEL_ARGS:imx8qm-mek ?= "pci=nomsi console=ttyLP0,115200 earlycon ${OSTREE_KERNEL_ARGS_COMMON}"
PREFERRED_PROVIDER_virtual/kernel:imx8qm-mek ?= "linux-lmp-fslc-imx"
KERNEL_DANGLING_FEATURES_WARN_ONLY:imx8qm-mek ?= "1"
UBOOT_CONFIG:imx8qm-mek = ""
UBOOT_SUFFIX:imx8qm-mek = "bin"
UBOOT_MACHINE:imx8qm-mek = "imx8qm_mek_defconfig"
UBOOT_DTB_NAME:imx8qm-mek ?= "fsl-imx8qm-mek.dtb"
## Toradex Apalis iMX8QM
OSTREE_KERNEL_ARGS:apalis-imx8 ?= "pci=nomsi console=tty1 console=ttyLP1,115200 earlycon ${OSTREE_KERNEL_ARGS_COMMON}"
PREFERRED_PROVIDER_virtual/kernel:apalis-imx8 ?= "linux-lmp-toradex-imx"
UBOOT_CONFIG:apalis-imx8 = ""
UBOOT_SUFFIX:apalis-imx8 = "bin"
UBOOT_MACHINE:apalis-imx8 = "apalis-imx8_defconfig"

# Toradex Apalis iMX8 with secure boot support
SOTA_CLIENT_FEATURES:remove:sota:apalis-imx8-sec = "ubootenv"

# iMX8MQ
UBOOT_SIGN_ENABLE:sota:mx8mq-generic-bsp = "1"
IMX_DEFAULT_BOOTLOADER:mx8mq-generic-bsp ?= "u-boot-fio"
UBOOT_DTB_LOADADDRESS:mx8mq-generic-bsp = "0x43000000"
PREFERRED_PROVIDER_virtual/trusted-firmware-a:mx8mq-nxp-bsp ?= "imx-atf"
EXTRA_IMAGEDEPENDS:append:mx8mq-generic-bsp = "virtual/trusted-firmware-a"
UBOOT_CLASSES:mx8mq-generic-bsp = "uboot-fitimage"
ATF_BINARY:mx8mq-generic-bsp = "arm-trusted-firmware.bin"
UBOOT_CONFIG:mx8mq-generic-bsp = ""
LMP_BOOT_FIRMWARE_FILES:mx8mq-nxp-bsp = "imx-boot imx-boot-nohdmi u-boot.itb"
IMAGE_BOOT_FILES:mx8mq-nxp-bsp = "imx-boot u-boot.itb ${KERNEL_IMAGETYPE} ${@make_dtb_boot_files(d)} boot.scr uEnv.txt"
IMAGE_BOOT_FILES:sota:mx8mq-nxp-bsp = "imx-boot u-boot.itb boot.itb"
WKS_FILE_DEPENDS:append:mx8mq-generic-bsp = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:mx8mq-generic-bsp = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:mx8mq-generic-bsp = "u-boot-ostree-scr-fit"
KERNEL_IMAGETYPE:sota:mx8mq-generic-bsp = "fitImage"
KERNEL_CLASSES:sota:mx8mq-generic-bsp = " kernel-lmp-fitimage "
## iMX8: Use latest NXP BSP downstream kernel
PREFERRED_PROVIDER_virtual/kernel:mx8mq-nxp-bsp ?= "linux-lmp-fslc-imx"
WKS_FILE:sota:mx8mq-nxp-bsp = "sdimage-imx8-spl-sota.wks.in"
IMXBOOT_TARGETS:mx8mq-nxp-bsp = "flash_evk_spl"
## iMX8MQ EVK
KMACHINE:imx8mq-evk = "imx8mqevk"
OSTREE_KERNEL_ARGS:imx8mq-evk ?= "console=tty1 console=ttymxc0,115200 earlycon=ec_imx6q,0x30860000,115200 root=/dev/mmcblk0p2 rootfstype=ext4"
SOTA_CLIENT_FEATURES:append:imx8mq-evk = " ubootenv"
MACHINE_FEATURES:remove:imx8mq-evk = "qca6174"
MACHINE_FEATURES:append:imx8mq-evk = " bcm4356"
UBOOT_MACHINE:imx8mq-evk ?= "imx8mq_evk_config"
BOOTSCR_LOAD_ADDR:imx8mq-evk = "0x44800000"
## iMX8MQ EVK with EBBR / SystemReady
UBOOT_SIGN_ENABLE:sota:imx8mq-evk-ebbr = ""
KERNEL_IMAGETYPE:sota:imx8mq-evk-ebbr ?= "Image"
KERNEL_CLASSES:sota:imx8mq-evk-ebbr ?= ""
EFI_PROVIDER:imx8mq-evk-ebbr ?= "grub-efi"
OSTREE_BOOTLOADER:imx8mq-evk-ebbr ?= "grub"
IMAGE_EFI_BOOT_FILES:sota:imx8mq-evk-ebbr ?= "${@make_efi_dtb_boot_files(d)}"
WKS_FILE:sota:imx8mq-evk-ebbr ?= "efidisk-sota.wks.in"

# iMX8MM
UBOOT_SIGN_ENABLE:sota:mx8mm-generic-bsp = "1"
IMX_DEFAULT_BOOTLOADER:mx8mm-generic-bsp ?= "u-boot-fio"
UBOOT_DTB_LOADADDRESS:mx8mm-generic-bsp = "0x43000000"
PREFERRED_PROVIDER_virtual/trusted-firmware-a:mx8mm-nxp-bsp ?= "imx-atf"
EXTRA_IMAGEDEPENDS:append:mx8mm-nxp-bsp = "virtual/trusted-firmware-a"
UBOOT_CLASSES:mx8mm-generic-bsp = "uboot-fitimage"
ATF_BINARY:mx8mm-generic-bsp = "arm-trusted-firmware.bin"
UBOOT_CONFIG:mx8mm-generic-bsp = ""
LMP_BOOT_FIRMWARE_FILES:mx8mm-nxp-bsp = "imx-boot u-boot.itb"
IMAGE_BOOT_FILES:mx8mm-nxp-bsp = "imx-boot u-boot.itb ${KERNEL_IMAGETYPE} ${@make_dtb_boot_files(d)} boot.scr uEnv.txt"
IMAGE_BOOT_FILES:sota:mx8mm-nxp-bsp = "boot.itb"
WKS_FILE_DEPENDS:append:mx8mm-generic-bsp = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:mx8mm-generic-bsp = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:mx8mm-generic-bsp = "u-boot-ostree-scr-fit"
KERNEL_IMAGETYPE:sota:mx8mm-generic-bsp = "fitImage"
KERNEL_CLASSES:sota:mx8mm-generic-bsp = " kernel-lmp-fitimage "
## iMX8: Use latest NXP BSP downstream kernel
PREFERRED_PROVIDER_virtual/kernel:mx8mm-nxp-bsp ?= "linux-lmp-fslc-imx"
MACHINE_FIRMWARE:mx8mm-nxp-bsp = "linux-firmware-imx-sdma-imx7d"
WKS_FILE:sota:mx8mm-nxp-bsp = "sdimage-imx8-spl-sota.wks.in"
IMXBOOT_TARGETS:mx8mm-nxp-bsp = "flash_evk_spl"
## iMX8MM EVK
KERNEL_DEVICETREE:append:imx8mm-lpddr4-evk = " freescale/imx8mm-evkb.dtb"
KMACHINE:imx8mm-lpddr4-evk = "imx8mmevk"
OSTREE_KERNEL_ARGS:imx8mm-lpddr4-evk ?= "console=tty1 console=ttymxc1,115200 earlycon=ec_imx6q,0x30890000,115200 root=/dev/mmcblk2p2 rootfstype=ext4"
PREFERRED_PROVIDER_virtual/dtb:imx8mm-lpddr4-evk ?= "lmp-device-tree"
SOTA_CLIENT_FEATURES:append:imx8mm-lpddr4-evk = " ubootenv"
UBOOT_MACHINE:imx8mm-lpddr4-evk = "imx8mm_evk_config"
BOOTSCR_LOAD_ADDR:imx8mm-lpddr4-evk = "0x44800000"
## iMX8MM EVK with secure boot support
SOTA_CLIENT_FEATURES:remove:sota:imx8mm-lpddr4-evk-sec = "ubootenv"
## iMX8MM EVK with EBBR / SystemReady
UBOOT_SIGN_ENABLE:sota:imx8mm-lpddr4-evk-ebbr = ""
KERNEL_IMAGETYPE:sota:imx8mm-lpddr4-evk-ebbr ?= "Image"
KERNEL_CLASSES:sota:imx8mm-lpddr4-evk-ebbr ?= ""
EFI_PROVIDER:imx8mm-lpddr4-evk-ebbr ?= "grub-efi"
OSTREE_BOOTLOADER:imx8mm-lpddr4-evk-ebbr ?= "grub"
IMAGE_EFI_BOOT_FILES:sota:imx8mm-lpddr4-evk-ebbr ?= "${@make_efi_dtb_boot_files(d)}"
WKS_FILE:sota:imx8mm-lpddr4-evk-ebbr ?= "efidisk-sota.wks.in"
# iMX8MM EVKB revision
MACHINE_FEATURES:append:imx8mm-lpddr4-evk = " nxp89xx mxm-mwifiex-load"

# iMX8MP
UBOOT_SIGN_ENABLE:sota:mx8mp-generic-bsp = "1"
IMX_DEFAULT_BOOTLOADER:mx8mp-generic-bsp ?= "u-boot-fio"
UBOOT_DTB_LOADADDRESS:mx8mp-generic-bsp = "0x43000000"
PREFERRED_PROVIDER_virtual/trusted-firmware-a:mx8mp-nxp-bsp ?= "imx-atf"
EXTRA_IMAGEDEPENDS:append:mx8mp-nxp-bsp = "virtual/trusted-firmware-a"
UBOOT_CLASSES:mx8mp-generic-bsp = "uboot-fitimage"
ATF_BINARY:mx8mp-generic-bsp = "arm-trusted-firmware.bin"
UBOOT_CONFIG:mx8mp-generic-bsp = ""
LMP_BOOT_FIRMWARE_FILES:mx8mp-nxp-bsp = "imx-boot u-boot.itb"
IMAGE_BOOT_FILES:mx8mp-nxp-bsp = "imx-boot u-boot.itb ${KERNEL_IMAGETYPE} ${@make_dtb_boot_files(d)} boot.scr uEnv.txt"
IMAGE_BOOT_FILES:sota:mx8mp-nxp-bsp = "boot.itb"
WKS_FILE_DEPENDS:append:mx8mp-generic-bsp = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:mx8mp-generic-bsp = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:mx8mp-generic-bsp = "u-boot-ostree-scr-fit"
KERNEL_IMAGETYPE:sota:mx8mp-generic-bsp = "fitImage"
KERNEL_CLASSES:sota:mx8mp-generic-bsp = " kernel-lmp-fitimage "
## iMX8: Use latest NXP BSP downstream kernel
PREFERRED_PROVIDER_virtual/kernel:mx8mp-nxp-bsp ?= "linux-lmp-fslc-imx"
MACHINE_FIRMWARE:mx8mp-nxp-bsp = "linux-firmware-imx-sdma-imx7d"
WKS_FILE:sota:mx8mp-nxp-bsp = "sdimage-imx8-spl-sota.wks.in"
IMXBOOT_TARGETS:mx8mp-nxp-bsp = "flash_evk_spl"
## iMX8MP EVK
OSTREE_KERNEL_ARGS:imx8mp-lpddr4-evk ?= "console=tty1 console=ttymxc1,115200 earlycon=ec_imx6q,0x30890000,115200 root=/dev/mmcblk2p2 rootfstype=ext4"
SOTA_CLIENT_FEATURES:append:imx8mp-lpddr4-evk = " ubootenv"
UBOOT_MACHINE:imx8mp-lpddr4-evk = "imx8mp_evk_config"
BOOTSCR_LOAD_ADDR:imx8mp-lpddr4-evk = "0x44800000"
## iMX8MP EVK with secure boot support
SOTA_CLIENT_FEATURES:remove:sota:imx8mp-lpddr4-evk-sec = "ubootenv"
## iMX8MP EVK with EBBR / SystemReady
UBOOT_SIGN_ENABLE:sota:imx8mp-lpddr4-evk-ebbr = ""
KERNEL_IMAGETYPE:sota:imx8mp-lpddr4-evk-ebbr ?= "Image"
KERNEL_CLASSES:sota:imx8mp-lpddr4-evk-ebbr ?= ""
EFI_PROVIDER:imx8mp-lpddr4-evk-ebbr ?= "grub-efi"
OSTREE_BOOTLOADER:imx8mp-lpddr4-evk-ebbr ?= "grub"
IMAGE_EFI_BOOT_FILES:sota:imx8mp-lpddr4-evk-ebbr ?= "${@make_efi_dtb_boot_files(d)}"
WKS_FILE:sota:imx8mp-lpddr4-evk-ebbr ?= "efidisk-sota.wks.in"
# imx8mp comes with an AzureWave CM276MA PCIe Card
MACHINE_FEATURES:append:imx8mp-lpddr4-evk = " nxp89xx mxm-mwifiex-load"

# STM32MP1
PREFERRED_PROVIDER_virtual/kernel:stm32mp1common ?= "linux-lmp"
PREFERRED_PROVIDER_virtual/bootloader:stm32mp1common = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:stm32mp1common = "u-boot-fio"
MACHINE_EXTRA_RRECOMMENDS:remove:stm32mp1common = "u-boot-fw-config-stm32mp"
KERNEL_IMAGETYPE:stm32mp1common = "fitImage"
KERNEL_CLASSES:stm32mp1common = " kernel-lmp-fitimage "
OSTREE_KERNEL:stm32mp1common = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_COMMON:stm32mp1common = "console=ttySTM0,115200 root=LABEL=otaroot rootfstype=ext4"
SERIAL_CONSOLES:stm32mp1common = "115200;ttySTM0"
PREFERRED_VERSION_gcc-arm-none-eabi-native:stm32mp1common = "9"
IMAGE_CLASSES:remove:stm32mp1common = "st-partitions-image"
IMAGE_CLASSES:remove:stm32mp1common = "image_types-stubi"
ENABLE_FLASHLAYOUT_CONFIG:stm32mp1common = "0"
IMAGE_ROOTFS_MAXSIZE:stm32mp1common = ""
KERNEL_BUILTIN_WIREGUARD:stm32mp1common = "1"
PREFERRED_PROVIDER_virtual/optee-os:stm32mp1common = "optee-os-fio"
MACHINE_EXTRA_RRECOMMENDS:remove:stm32mp1common = "kernel-imagebootfs"
BOOTSCHEME_LABELS:stm32mp1common += "optee"
BOOTSCHEME_LABELS:stm32mp1common:remove = "trusted"
UBOOT_CONFIG:remove:stm32mp1common = "basic_stm32mp15"
UBOOT_SUFFIX:stm32mp1common = "bin"
## stm32mp15-eval
LINUX_A7_EXAMPLES_DT:remove:stm32mp15-eval = "stm32mp157c-ev1-a7-examples"
CUBE_M4_EXAMPLES_DT:remove:stm32mp15-eval = "stm32mp157c-ev1-m4-examples"
UBOOT_DEVICETREE:stm32mp15-eval = "stm32mp157a-ev1"
IMAGE_FSTYPES:remove:stm32mp15-eval = "wic wic.gz wic.bmap"
EXTRA_IMAGEDEPENDS:append:stm32mp15-eval = "flashlayouts-stm32mp1"
UBOOT_LOADADDRESS:stm32mp15-eval = "0xC2000000"
UBOOT_ENTRYPOINT:stm32mp15-eval = "0xC2000000"
UBOOT_DTB_LOADADDRESS:stm32mp15-eval = "0xC4000000"
UBOOT_RD_LOADADDRESS:stm32mp15-eval = "0xC4400000"
UBOOT_RD_ENTRYPOINT:stm32mp15-eval = "0xC4400000"
LMP_FLASHLAYOUT:stm32mp15-eval = "FlashLayout_emmc_stm32mp157a-ev1-optee.tsv"
## stm32mp15-disco
LINUX_A7_EXAMPLES_DT:remove:stm32mp15-disco = "stm32mp157c-dk2-a7-examples"
CUBE_M4_EXAMPLES_DT:remove:stm32mp15-disco = "stm32mp157c-dk2-m4-examples"
UBOOT_DEVICETREE:stm32mp15-disco = "stm32mp157c-dk2"
ST_BOOTFS:stm32mp15-disco = "0"
ST_VENDORFS:stm32mp15-disco = "0"
ST_USERFS:stm32mp15-disco = "0"
M4_INSTALLDIR:stm32mp15-disco = "${prefix}"
SOTA_CLIENT_FEATURES:append:stm32mp15-disco = " ubootenv"
UBOOT_LOADADDRESS:stm32mp15-disco = "0xC2000000"
UBOOT_ENTRYPOINT:stm32mp15-disco = "0xC2000000"
UBOOT_DTB_LOADADDRESS:stm32mp15-disco = "0xC4000000"
IMAGE_BOOT_FILES:stm32mp15-disco = "boot.itb"
EXTRA_IMAGEDEPENDS:remove:stm32mp15-disco = "sdcard-raw-tools-native"
WKS_FILE_DEPENDS:append:stm32mp15-disco = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:stm32mp15-disco = "u-boot-ostree-scr-fit"
WKS_FILE_DEPENDS:remove:stm32mp15-disco = "st-image-bootfs st-image-userfs"
WKS_FILE:sota:stm32mp15-disco = "sdimage-stm32mp157c-dk2-optee-sota.wks.in"

# Xilinx
USE_XSCT_TARBALL:zynqmp = "1"
KERNEL_BUILTIN_WIREGUARD:zynqmp = "1"
## Avnet UltraZed with secure boot support
UBOOT_SIGN_ENABLE:sota:uz3eg-iocc-sec = "1"
SOTA_CLIENT_FEATURES:remove:sota:uz3eg-iocc-sec = "ubootenv"
## Avnet UltraZed with secure boot support and EBBR
UBOOT_SPL_SIGN_ENABLE:sota:uz3eg-iocc-ebbr-sec = "1"
UBOOT_SIGN_ENABLE:sota:uz3eg-iocc-ebbr-sec = "0"

## Xilinx Versal
USE_XSCT_TARBALL:versal = "1"
PREFERRED_PROVIDER_u-boot:versal = "u-boot-xlnx"
PREFERRED_PROVIDER_u-boot-fw-utils:versal = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils:versal = "libubootenv"
SOTA_CLIENT_FEATURES:append:versal = " ubootenv"
WKS_FILE_DEPENDS:append:sota:versal = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:versal = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script:sota:versal = "u-boot-ostree-scr-fit"
PREFERRED_PROVIDER_virtual/kernel:versal = "linux-lmp-xlnx"
PREFERRED_PROVIDER_virtual/dtb:versal = "device-tree"
PREFERRED_PROVIDER_virtual/trusted-firmware-a:versal = "arm-trusted-firmware"
MACHINE_FEATURES:append:versal = " optee"
KERNEL_BUILTIN_WIREGUARD:versal = "1"
EXTRA_IMAGEDEPENDS:remove:versal = "qemu-devicetrees"
UBOOT_ENTRYPOINT:versal = "0x18000000"
OSTREE_KERNEL_ARGS:versal ?= "console=ttyAMA0,115200 earlycon=pl011,mmio32,0xFF000000,115200n8 clk_ignore_unused ${OSTREE_KERNEL_ARGS_COMMON}"
KERNEL_IMAGETYPE:sota:versal = "fitImage"
KERNEL_CLASSES:sota:versal = "kernel-lmp-fitimage"
IMAGE_BOOT_FILES:sota:versal = "boot.bin boot.itb"
WKS_FILES:sota:versal = "sdimage-sota.wks"

# TI AM64x
MACHINE_FEATURES:append:am64xx-evm = " optee"
SOTA_CLIENT_FEATURES:append:am64xx-evm = " ubootenv"
KERNEL_BUILTIN_WIREGUARD:am64xx-evm = "1"
PREFERRED_PROVIDER_virtual/kernel:am64xx-evm ?= "linux-lmp-ti-staging"
PREFERRED_PROVIDER_u-boot-fw-utils:am64xx-evm = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils:am64xx-evm = "libubootenv"
# only need boot script for sota builds
WKS_FILE_DEPENDS:append:sota:am64xx-evm = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:sota:am64xx-evm = "u-boot-ostree-scr-fit"
KERNEL_IMAGETYPE:sota:am64xx-evm = "fitImage"
KERNEL_CLASSES:sota:am64xx-evm = " kernel-lmp-fitimage "
IMAGE_BOOT_FILES:append:am64xx-evm = " boot.itb"
UBOOT_ENTRYPOINT:am64xx-evm = "0x82000000"
UBOOT_LOADADDRESS:am64xx-evm = "0x82000000"
UBOOT_DTB_LOADADDRESS:am64xx-evm = "0x88000000"
EFI_PROVIDER:am64xx-evm = ""
# multiconfig with the same TMP fails in the rm_work.bbclass
RM_WORK_EXCLUDE:append:am64xx-evm = " texinfo-dummy-native gettext-minimal-native gnu-config"

# Nvidia Tegra
DISTRO_FEATURES:append:tegra = " opengl"
DISTRO_FEATURES_DEFAULT:remove:tegra = "modsign"
INITRAMFS_IMAGE_BUNDLE:tegra = ""
PREFERRED_PROVIDER_virtual/kernel:tegra ?= "linux-tegra"
IMAGE_TEGRAFLASH_FS_TYPE:tegra = "ota-ext4"
IMAGE_FSTYPES:append:tegra = " tegraflash"
IMAGE_FSTYPES:remove:tegra = "wic wic.gz wic.bmap"
## jetson-agx-xavier-devkit (tegra194)
OSTREE_DEPLOY_DEVICETREE:tegra194 = "1"
OSTREE_KERNEL_ARGS:tegra194 ?= "\${cbootargs} ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_BOOTLOADER:tegra194 = "syslinux"

# Allwinner (sun8i)
PREFERRED_PROVIDER_virtual/bootloader:sun8i = "u-boot-fio"
PREFERRED_PROVIDER_u-boot:sun8i = "u-boot-fio"
UBOOT_ENTRYPOINT:sun8i = "0x42000000"
WKS_FILE_DEPENDS:append:sun8i = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script:sun8i = "u-boot-ostree-scr-fit"
IMAGE_BOOT_FILES:sun8i = "boot.itb"
KERNEL_IMAGETYPE:sun8i = "fitImage"
KERNEL_CLASSES:sun8i = " kernel-lmp-fitimage "
WKS_FILES:sota:sun8i = "sdimage-sunxi-sota.wks.in"
IMAGE_FSTYPES:remove:sun8i = "sunxi-sdimg"
SOTA_CLIENT_FEATURES:append:sun8i = " ubootenv"
OSTREE_KERNEL_ARGS:sun8i ?= "earlycon console=ttyS0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"

# Cross machines / BSPs
## iMX targets should use the u-boot release based on the NXP BSP
PREFERRED_VERSION_u-boot-fio:imx-nxp-bsp ?= "2021.04"
## Prefer OP-TEE releases from our layer instead of using the .imx fork
PREFERRED_VERSION_optee-client:mx8-nxp-bsp = "3.17.0"
PREFERRED_VERSION_optee-examples:mx8-nxp-bsp = "3.17.0"
PREFERRED_VERSION_optee-test:mx8-nxp-bsp = "3.17.0"
## We don't use imx-boot-container just yet
UBOOT_PROVIDES_BOOT_CONTAINER:mx8-generic-bsp = ""
## No need to install u-boot, already a WKS dependency
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:remove:imx-generic-bsp = "u-boot-fslc"
## OP-TEE is a dependency of u-boot (fit), no need for WKS_FILE_DEPENDS
OPTEE_WKS_FILE_DEPENDS:imx-generic-bsp = ""
