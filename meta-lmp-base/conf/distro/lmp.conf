require conf/distro/include/lmp.inc

DISTRO = "lmp"
DISTRO_NAME = "Linux-microPlatform"

IMAGE_LINGUAS ?= "en-us"

# SOTA
OSTREE_OSNAME = "lmp"
INITRAMFS_IMAGE = "initramfs-ostree-lmp-image"
INITRAMFS_FSTYPES = "cpio.gz"
OSTREE_KERNEL ?= "${@oe.utils.conditional('KERNEL_IMAGETYPE', 'fitImage', '${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${KERNEL_FIT_LINK_NAME}', '${KERNEL_IMAGETYPE}', d)}"
OSTREE_KERNEL_ARGS_COMMON ?= "root=LABEL=otaroot rootfstype=ext4"
OSTREE_KERNEL_ARGS ?= "${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_SPLIT_BOOT ?= "0"
OSTREE_LOADER_LINK ?= "1"
OSTREE_DEPLOY_USR_OSTREE_BOOT ?= "0"
DISTRO_FEATURES:append = " sota"
DISTRO_FEATURES_NATIVE:append = " sota"
INHERIT += "sota"
## No need to install the kernel image into the boot partition
IMAGE_BOOT_FILES:remove = " ${KERNEL_IMAGETYPE}"
## Prefer aktualizr-lite as the default SOTA_CLIENT
SOTA_CLIENT ?= "aktualizr-lite"
## Disable default prov by default to allow runtime configuration
SOTA_CLIENT_PROV ?= ""
## Also produce OTA rootfs tarball for additional image support
BUILD_OTA_TARBALL = "1"
## Support Aktualizr with HSM by default for secure SOTA
SOTA_CLIENT_FEATURES:append = " hsm"
## Sota BSP specific configs are managed by LMP
SOTA_MACHINE:lmp = "lmp"
## Allow custom SOTA_CLIENT providers (e.g. aktualizr-lite)
IMAGE_INSTALL:append = " ${@oe.utils.ifelse('${SOTA_CLIENT}' != 'aktualizr', '${SOTA_CLIENT}', '')}"
IMAGE_INSTALL:remove = "${@oe.utils.ifelse('${SOTA_CLIENT}' != 'aktualizr', 'aktualizr aktualizr-info', '')}"
## Do not ship repo manifest in /etc by default (published by CI instead)
IMAGE_CLASSES:remove = "image_repo_manifest"
## No need for fstab update as it is available via the ostree rootfs
WIC_CREATE_EXTRA_ARGS:sota:append = " --no-fstab-update"
## SOTA credentials requires java from the host
HOSTTOOLS += "${@'java' if d.getVar('SOTA_PACKED_CREDENTIALS') else ''}"

# Prelinking increases the size of downloads and causes build errors
USER_CLASSES:remove = "image-prelink"

# For live/installer images
INITRD_IMAGE_LIVE ?= "core-image-minimal-initramfs"
LIVE_ROOTFS_TYPE = "ota-ext4"
LABELS_LIVE = "install"

# General
WKS_FILE_DEPENDS_BOOTLOADERS:remove = "syslinux"

# WKS partition default configuration
OSTREE_WKS_EFI_SIZE ?= "--size 512M"
OSTREE_WKS_BOOT_SIZE ?= "--size 200M --overhead-factor 1"
