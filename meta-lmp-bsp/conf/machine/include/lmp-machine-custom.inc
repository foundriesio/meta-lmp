# LMP specific configuration

# Beaglebone
PREFERRED_PROVIDER_virtual/bootloader_beaglebone-yocto = "u-boot-fio"
PREFERRED_PROVIDER_u-boot_beaglebone-yocto = "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_beaglebone-yocto ?= "2020.04"
WKS_FILE_DEPENDS_append_beaglebone-yocto = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_beaglebone-yocto = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES_append_beaglebone-yocto = " ubootenv"
OSTREE_KERNEL_ARGS_beaglebone-yocto ?= "console=ttyS0,115200n8 ${OSTREE_KERNEL_ARGS_COMMON}"
KERNEL_DEVICETREE_append_beaglebone-yocto = " am335x-boneblack-wireless.dtb"
IMAGE_BOOT_FILES_beaglebone-yocto = "u-boot.img MLO boot.itb"
KERNEL_IMAGETYPE_beaglebone-yocto = "fitImage"
KERNEL_CLASSES_beaglebone-yocto = " kernel-lmp-fitimage "
OSTREE_KERNEL_beaglebone-yocto = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
## beaglebone-yocto.conf appends kernel-image-zimage by default
IMAGE_INSTALL_remove_beaglebone-yocto = "kernel-image-zimage"

# Raspberry Pi
PREFERRED_PROVIDER_virtual/bootloader_rpi = "u-boot-fio"
PREFERRED_PROVIDER_u-boot_rpi = "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_rpi ?= "2020.04"
SOTA_CLIENT_FEATURES_append_rpi = " ubootenv"
WKS_FILE_DEPENDS_BOOTLOADERS_rpi = "virtual/bootloader"
WKS_FILE_DEPENDS_append_rpi = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_rpi = "u-boot-ostree-scr"
PREFERRED_PROVIDER_virtual/dtb_rpi ?= "lmp-device-tree"
IMAGE_FSTYPES_remove_rpi = "ext3 rpi-sdimg"
IMAGE_BOOT_FILES_rpi = "bcm2835-bootfiles/* u-boot.bin;${SDIMG_KERNELIMAGE} ${@make_dtb_boot_files(d)} boot.scr uEnv.txt"
KERNEL_CLASSES_rpi = " kernel-lmp-fitimage "
OSTREE_KERNEL_rpi = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
KERNEL_IMAGETYPE_rpi = "fitImage"
KERNEL_DEVICETREE_COMMON_RPI ?= "overlays/vc4-kms-v3d.dtbo overlays/vc4-fkms-v3d.dtbo overlays/rpi-ft5406.dtbo overlays/disable-bt.dtbo overlays/uart0.dtbo overlays/mcp2515-can0.dtbo"
KERNEL_DEVICETREE_raspberrypi3 ?= "bcm2710-rpi-3-b.dtb bcm2710-rpi-3-b-plus.dtb bcm2710-rpi-cm3.dtb ${KERNEL_DEVICETREE_COMMON_RPI}"
KERNEL_DEVICETREE_raspberrypi3-64 ?= "broadcom/bcm2710-rpi-3-b.dtb broadcom/bcm2710-rpi-3-b-plus.dtb broadcom/bcm2710-rpi-cm3.dtb ${KERNEL_DEVICETREE_COMMON_RPI}"
KERNEL_DEVICETREE_raspberrypi-cm3 ?= "bcm2710-rpi-cm3.dtb ${KERNEL_DEVICETREE_COMMON_RPI}"
KERNEL_DEVICETREE_raspberrypi4 ?= "bcm2711-rpi-4-b.dtb ${KERNEL_DEVICETREE_COMMON_RPI}"
KERNEL_DEVICETREE_raspberrypi4-64 ?= "broadcom/bcm2711-rpi-4-b.dtb ${KERNEL_DEVICETREE_COMMON_RPI}"
## Mimic meta-raspberrypi behavior
KERNEL_SERIAL_rpi ?= "${@oe.utils.conditional("ENABLE_UART", "1", "console=ttyS0,115200", "", d)}"
KERNEL_SERIAL_raspberrypi-cm3 ?= "console=ttyAMA0,115200"
OSTREE_KERNEL_ARGS_COMMON_RPI ?= "coherent_pool=1M 8250.nr_uarts=1 dwc_otg.lpm_enable=0 console=tty1 ${KERNEL_SERIAL} ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS_raspberrypi3 ?= "cma=256M vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 ${OSTREE_KERNEL_ARGS_COMMON_RPI}"
OSTREE_KERNEL_ARGS_raspberrypi-cm3 ?= "cma=256M vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 ${OSTREE_KERNEL_ARGS_COMMON_RPI}"
OSTREE_KERNEL_ARGS_raspberrypi4 ?= "video=HDMI-A-1:1280x720@60 cma=256M vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 ${OSTREE_KERNEL_ARGS_COMMON_RPI}"
## U-Boot entrypoints for rpi
UBOOT_ENTRYPOINT_rpi = "0x00008000"
UBOOT_DTB_LOADADDRESS_rpi = "0x02600000"
UBOOT_DTBO_LOADADDRESS_rpi = "0x026d0000"
## RPI: Use official raspberrypi downstream kernel
PREFERRED_PROVIDER_virtual/kernel_rpi ?= "linux-lmp-dev"
LINUX_VERSION_rpi ?= "4.19.y"
KERNEL_REPO_rpi ?= "git://github.com/raspberrypi/linux.git"
KERNEL_BRANCH_rpi ?= "rpi-4.19.y"
KERNEL_COMMIT_rpi ?= "f6b3ac28f0a9137d4c24c0b8832e693bbd16f5b7"
KERNEL_META_BRANCH_rpi ?= "linux-v4.19.y"
KERNEL_META_COMMIT_rpi ?= "1e97ab775c4c499b995e7013ffc221d71844ba87"
MACHINE_FEATURES_append_raspberrypi4 = " armstub"

# RISC-V targets
INITRAMFS_IMAGE_BUNDLE_qemuriscv64 = "1"
KERNEL_INITRAMFS_qemuriscv64 = '-initramfs'
IMAGE_BOOT_FILES_qemuriscv64 = "boot.scr uEnv.txt"
KERNEL_IMAGETYPE_qemuriscv64 = "fitImage"
WKS_FILE_DEPENDS_append_qemuriscv64 = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_qemuriscv64 = "u-boot-ostree-scr"
KERNEL_IMAGETYPES_remove_qemuriscv64 = "uImage"
KERNEL_CLASSES_qemuriscv64 = " kernel-lmp-fitimage "
OSTREE_KERNEL_qemuriscv64 = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_qemuriscv64 ?= "earlycon=sbi ${OSTREE_KERNEL_ARGS_COMMON}"
UBOOT_ENTRYPOINT_qemuriscv64 = "0x80400000"
UBOOT_RD_LOADADDRESS_qemuriscv64 = "0x81000000"
RISCV_SBI_PAYLOAD_qemuriscv64 = "u-boot.bin"
QB_DEFAULT_BIOS_qemuriscv64 = "fw_payload.elf"
QB_DRIVE_TYPE_qemuriscv64 = "/dev/vdb"
QB_OPT_APPEND_append_qemuriscv64 = " -bios ${DEPLOY_DIR_IMAGE}/fw_payload.elf"

## Freedom U540
PREFERRED_PROVIDER_virtual/bootloader_freedom-u540 ?= "u-boot"
IMAGE_BOOT_FILES_freedom-u540 = "fw_payload.bin boot.scr uEnv.txt"
WKS_FILE_DEPENDS_append_freedom-u540 = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_freedom-u540 = "u-boot-ostree-scr"
OSTREE_KERNEL_freedom-u540 = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_freedom-u540 ?= "earlycon=sbi console=ttySIF0 ${OSTREE_KERNEL_ARGS_COMMON}"
WKS_FILE_freedom-u540_sota = "freedom-u540-opensbi-sota.wks"

# QEMU ARM
PREFERRED_PROVIDER_virtual/bootloader_qemuarm64 = "u-boot-fio"
PREFERRED_PROVIDER_u-boot_qemuarm64 = "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_qemuarm64 ?= "2020.04"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_append_qemuarm64 = "atf"
WKS_FILE_DEPENDS_append_qemuarm64 = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_qemuarm64 = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES_append_qemuarm64 = " ubootenv"
IMAGE_BOOT_FILES_qemuarm64 = "boot.itb"
KERNEL_IMAGETYPE_qemuarm64 = "fitImage"
KERNEL_CLASSES_qemuarm64 = " kernel-lmp-fitimage "
OSTREE_KERNEL_qemuarm64 = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_qemuarm64 ?= "console=ttyAMA0 ${OSTREE_KERNEL_ARGS_COMMON}"
UBOOT_ENTRYPOINT_qemuarm64 = "0x40080000"
MACHINE_FEATURES_append_qemuarm64 = " optee"
## ARM Trusted Firmware options
TF-A_PLATFORM_qemuarm64 = "qemu"
TF-A_DEPENDS_qemuarm64 = "u-boot"
TF-A_BL33_qemuarm64 = "${DEPLOY_DIR_IMAGE}/u-boot.bin"
TF-A_EXTRA_OPTIONS_qemuarm64 = " BL32=${DEPLOY_DIR_IMAGE}/optee/tee-header_v2.bin \
  BL32_EXTRA1=${DEPLOY_DIR_IMAGE}/optee/tee-pager_v2.bin \
  BL32_EXTRA2=${DEPLOY_DIR_IMAGE}/optee/tee-pageable_v2.bin \
  ARM_TSP_RAM_LOCATION=tdram BL32_RAM_LOCATION=tdram"
QB_MACHINE_qemuarm64 = "-machine virt,secure=on"
## Use same minimal memory amount as suggested by op-tee
QB_MEM_qemuarm64 = "-m 1057"
QB_DRIVE_TYPE_qemuarm64 = "/dev/vdb"
## Bios/bl1.bin is ATF, which requires semihosting for the remaining boot artifacts
QB_OPT_APPEND_qemuarm64 = "-no-acpi -bios bl1.bin -d unimp -semihosting-config enable,target=native"

# QEMU ARM
PREFERRED_PROVIDER_virtual/bootloader_qemuarm = "u-boot-fio"
PREFERRED_PROVIDER_u-boot_qemuarm = "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_qemuarm ?= "2020.04"
WKS_FILE_DEPENDS_append_qemuarm = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_qemuarm = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES_append_qemuarm = " ubootenv"
IMAGE_BOOT_FILES_qemuarm = "boot.itb"
KERNEL_IMAGETYPE_qemuarm = "fitImage"
KERNEL_CLASSES_qemuarm = " kernel-lmp-fitimage "
OSTREE_KERNEL_qemuarm = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_qemuarm ?= "console=ttyAMA0 ${OSTREE_KERNEL_ARGS_COMMON}"
UBOOT_ENTRYPOINT_qemuarm = "0x40080000"
QB_DRIVE_TYPE_qemuarm = "/dev/vdb"
## Currently support u-boot -> Linux boot
## will add more security later
QB_OPT_APPEND_qemuarm = "-no-acpi -bios u-boot.bin -d unimp"

# Intel
OSTREE_BOOTLOADER_intel-corei7-64 ?= "grub"
OSTREE_KERNEL_ARGS_intel-corei7-64 ?= "console=ttyS0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
EFI_PROVIDER_intel-corei7-64 = "grub-efi"
WKS_FILE_intel-corei7-64_sota = "efidisk-sota.wks"
IMAGE_BOOT_FILES_intel-corei7-64 = ""

# Common for iMX targets
## Prefer IMX_DEFAULT_BSP=nxp as mainline removes every common machine override
IMX_DEFAULT_BSP = "nxp"
OSTREE_KERNEL_ARGS_mx6 ?= "console=tty1 console=ttymxc0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS_mx6ull ?= "console=tty1 console=ttymxc0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS_mx7d ?= "console=tty1 console=ttymxc0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
OSTREE_KERNEL_ARGS_mx7ulp ?= "console=tty1 console=ttyLP0,115200 ${OSTREE_KERNEL_ARGS_COMMON}"

# cubox-i (hummingboard2)
KERNEL_DEVICETREE_append_cubox-i = " \
    imx6dl-hummingboard2-som-v15.dtb imx6q-hummingboard2-som-v15.dtb \
    imx6dl-hummingboard2-emmc-som-v15.dtb imx6q-hummingboard2-emmc-som-v15.dtb \
    imx6dl-hummingboard2.dtb imx6q-hummingboard2.dtb \
"
PREFERRED_PROVIDER_virtual/bootloader_cubox-i = "u-boot-fio"
PREFERRED_PROVIDER_u-boot_cubox-i = "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_cubox-i ?= "2020.04"
PREFERRED_PROVIDER_u-boot-fw-utils_cubox-i = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils_cubox-i = "libubootenv"
WKS_FILE_DEPENDS_append_cubox-i = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_cubox-i = "u-boot-ostree-scr-fit"
SOTA_CLIENT_FEATURES_append_cubox-i = " ubootenv"
IMAGE_BOOT_FILES_append_cubox-i = " boot.itb "
KERNEL_IMAGETYPE_cubox-i = "fitImage"
KERNEL_CLASSES_cubox-i = " kernel-lmp-fitimage "
OSTREE_KERNEL_cubox-i = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
UBOOT_CONFIG_cubox-i = ""
UBOOT_MACHINE_cubox-i = "mx6cuboxi_defconfig"
UBOOT_DTB_LOADADDRESS_cubox-i = "0x18000000"
UBOOT_RD_LOADADDRESS_cubox-i = "0x13000000"
WKS_FILES_sota_cubox-i = "sdimage-imx6-spl-sota.wks"
UBOOT_EXTLINUX_cubox-i = ""

# Toradex Colibri iMX7 eMMC
PREFERRED_PROVIDER_u-boot-default-script_colibri-imx7-emmc = "u-boot-ostree-scr"
PREFERRED_PROVIDER_virtual/bootloader_colibri-imx7-emmc = "u-boot-fio"
PREFERRED_PROVIDER_u-boot_colibri-imx7-emmc = "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_colibri-imx7-emmc ?= "2020.04"
PREFERRED_PROVIDER_u-boot-fw-utils_colibri-imx7-emmc = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils_colibri-imx7-emmc = "libubootenv"
IMAGE_BOOT_FILES_colibri-imx7-emmc = "uEnv.txt u-boot.imx"
KERNEL_IMAGETYPE_colibri-imx7-emmc = "fitImage"
KERNEL_CLASSES_colibri-imx7-emmc = " kernel-lmp-fitimage "
OSTREE_KERNEL_colibri-imx7-emmc = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
UBOOT_ENTRYPOINT_colibri-imx7-emmc = "0x81000000"
UBOOT_DTB_LOADADDRESS_colibri-imx7-emmc = "0x82000000"
UBOOT_RD_LOADADDRESS_colibri-imx7-emmc = "0x82100000"

# iMX6ULL
UBOOT_SIGN_ENABLE_mx6ull = "1"
PREFERRED_PROVIDER_virtual/kernel_mx6ull ?= "linux-lmp-fslc-imx"
PREFERRED_PROVIDER_virtual/bootloader_mx6ull ?= "u-boot-fio"
PREFERRED_PROVIDER_u-boot_mx6ull ?= "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_mx6ull ?= "2020.04"
PREFERRED_PROVIDER_u-boot-fw-utils_mx6ull = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils_mx6ull = "libubootenv"
SOTA_CLIENT_FEATURES_append_mx6ull = " ubootenv"
WKS_FILE_DEPENDS_append_mx6ull = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_mx6ull = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script_sota_mx6ull = "u-boot-ostree-scr-fit"
SPL_BINARY_mx6ull = "SPL"
UBOOT_CLASSES_mx6ull = "uboot-fitimage"
UBOOT_CONFIG_mx6ull = ""
UBOOT_SUFFIX_mx6ull = "bin"
UBOOT_MAKE_TARGET_mx6ull = ""
UBOOT_ENTRYPOINT_mx6ull  = "0x80008000"
UBOOT_DTB_LOADADDRESS_mx6ull = "0x85800000"
UBOOT_RD_LOADADDRESS_mx6ull = "0x84000000"
MACHINE_FEATURES_append_mx6ull = " optee"
IMAGE_BOOT_FILES_sota_mx6ull = " boot.itb SPL u-boot.itb "
IMAGE_BOOT_FILES_mx6ull = " boot.scr uEnv.txt zImage"
KERNEL_IMAGETYPE_sota_mx6ull = "fitImage"
KERNEL_CLASSES_sota_mx6ull = " kernel-lmp-fitimage "
OSTREE_KERNEL_mx6ull = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
WKS_FILE_mx6ull_sota = "sdimage-imx6-spl-fit-sota.wks"
PREFERRED_VERSION_optee-os-fio_mx6ull = "3.10.0+git"
PREFERRED_VERSION_optee-client_mx6ull = "3.10.0"
PREFERRED_VERSION_optee-examples_mx6ull = "3.10.0"
PREFERRED_VERSION_optee-test_mx6ull = "3.10.0"
OPTEE_BINARY_mx6ull = "tee-pager_v2.bin"
# iMX6ULL EVK
UBOOT_MACHINE_imx6ullevk = "mx6ull_14x14_evk_defconfig"
KERNEL_DEVICETREE_imx6ullevk = "imx6ull-14x14-evk.dtb"

# Toradex Apalis iMX6
UBOOT_SIGN_ENABLE_apalis-imx6 = "1"
PREFERRED_PROVIDER_u-boot-default-script_apalis-imx6 = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script_sota_apalis-imx6 = "u-boot-ostree-scr-fit"
IMAGE_BOOT_FILES_apalis-imx6 = "boot.scr uEnv.txt SPL u-boot.imx-spl"
IMAGE_BOOT_FILES_remove_apalis-imx6 = "boot.scr-${MACHINE};boot.scr"
IMAGE_BOOT_FILES_sota_apalis-imx6 = "boot.itb SPL u-boot.itb"
KERNEL_IMAGETYPE_sota_apalis-imx6 = "fitImage"
KERNEL_CLASSES_sota_apalis-imx6 = " kernel-lmp-fitimage "
OSTREE_KERNEL_apalis-imx6 = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_apalis-imx6 ?= "console=tty1 console=ttymxc0,115200 root=/dev/mmcblk2p2 rootfstype=ext4"
PREFERRED_PROVIDER_virtual/kernel_apalis-imx6 ?= "linux-lmp"
PREFERRED_PROVIDER_virtual/bootloader_apalis-imx6 ?= "u-boot-fio"
PREFERRED_PROVIDER_u-boot_apalis-imx6 ?= "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_apalis-imx6 ?= "2020.04"
PREFERRED_PROVIDER_u-boot-fw-utils_apalis-imx6 = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils_apalis-imx6 = "libubootenv"
SOTA_CLIENT_FEATURES_append_apalis-imx6 = " ubootenv"
UBOOT_CONFIG_apalis-imx6 = ""
UBOOT_SUFFIX_apalis-imx6 = "bin"
UBOOT_MACHINE_apalis-imx6 = "apalis_imx6_defconfig"
UBOOT_CLASSES_apalis-imx6 = "uboot-fitimage"
UBOOT_DTB_LOADADDRESS_apalis-imx6 = "0x15800000"
UBOOT_RD_LOADADDRESS_apalis-imx6 = "0x14000000"
WKS_FILES_sota_apalis-imx6 = "sdimage-imx6dq-spl-fit-sota.wks"
MACHINE_FEATURES_append_apalis-imx6 = " optee"
PREFERRED_VERSION_optee-os-fio_apalis-imx6 = "3.10.0+git"
PREFERRED_VERSION_optee-client_apalis-imx6 = "3.10.0"
PREFERRED_VERSION_optee-examples_apalis-imx6 = "3.10.0"
PREFERRED_VERSION_optee-test_apalis-imx6 = "3.10.0"
OPTEE_BINARY_apalis-imx6 = "tee-pager_v2.bin"

# Toradex Apalis iMX8QM
UBOOT_DTB_LOADADDRESS_apalis-imx8 = "0x84000000"
UBOOT_RD_LOADADDRESS_apalis-imx8 = ""
IMAGE_BOOT_FILES_append_apalis-imx8 = " uEnv-lmp.txt imx-boot"
KERNEL_IMAGETYPE_sota_apalis-imx8 = "fitImage"
KERNEL_CLASSES_sota_apalis-imx8 = " kernel-lmp-fitimage "
OSTREE_KERNEL_mx8qm = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_apalis-imx8 ?= "console=tty1 console=ttyLP1,115200 earlycon=lpuart32,0x5a070000,115200 ${OSTREE_KERNEL_ARGS_COMMON}"
## iMX8: Use latest Toradex BSP downstream kernel
PREFERRED_PROVIDER_virtual/kernel_apalis-imx8 ?= "linux-lmp-dev"
WKS_FILE_apalis-imx8_sota = "sdimage-imx8-sota.wks.in"
PREFERRED_PROVIDER_u-boot-default-script_apalis-imx8 = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script_sota_apalis-imx8 = "u-boot-ostree-scr"
IMAGE_CLASSES_remove_apalis-imx8 = "image_type_tezi"
IMAGE_FSTYPES_remove_apalis-imx8 = "teziimg"

# iMX8MM
UBOOT_SIGN_ENABLE_mx8mm = "1"
PREFERRED_PROVIDER_virtual/bootloader_mx8mm ?= "u-boot-fio"
PREFERRED_PROVIDER_u-boot_mx8mm ?= "u-boot-fio"
PREFERRED_VERSION_u-boot-fio_mx8mm ?= "2020.04"
PREFERRED_PROVIDER_u-boot-fw-utils_mx8mm = "libubootenv"
PREFERRED_RPROVIDER_u-boot-fw-utils_mx8mm = "libubootenv"
SOTA_CLIENT_FEATURES_append_mx8mm = " ubootenv"
UBOOT_DTB_LOADADDRESS_mx8mm = "0x43000000"
IMAGE_BOOT_FILES_mx8mm = "imx-boot boot.scr uEnv.txt"
IMAGE_BOOT_FILES_sota_mx8mm = "imx-boot boot.itb"
WKS_FILE_DEPENDS_append_mx8mm = " u-boot-default-script"
PREFERRED_PROVIDER_u-boot-default-script_mx8mm = "u-boot-base-scr"
PREFERRED_PROVIDER_u-boot-default-script_sota_mx8mm = "u-boot-ostree-scr-fit"
KERNEL_IMAGETYPE_sota_mx8mm = "fitImage"
KERNEL_CLASSES_sota_mx8mm = " kernel-lmp-fitimage "
OSTREE_KERNEL_mx8mm = "${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${MACHINE}"
OSTREE_KERNEL_ARGS_imx8mmevk ?= "console=tty1 console=ttymxc1,115200 earlycon=ec_imx6q,0x30890000,115200 root=/dev/mmcblk2p2 rootfstype=ext4"
PREFERRED_PROVIDER_virtual/dtb_imx8mmevk ?= "lmp-device-tree"
## iMX8: Use latest NXP BSP downstream kernel
PREFERRED_PROVIDER_virtual/kernel_mx8mm ?= "linux-lmp-fslc-imx"
MACHINE_FIRMWARE_mx8mm = "linux-firmware-imx-sdma-imx7d"
WKS_FILE_mx8mm_sota = "sdimage-imx8-sota.wks.in"
## iMX8MM EVK
PREFERRED_VERSION_optee-os-fio_imx8mmevk = "3.10.0+git"
PREFERRED_VERSION_optee-client_imx8mmevk = "3.10.0"
PREFERRED_VERSION_optee-examples_imx8mmevk = "3.10.0"
PREFERRED_VERSION_optee-test_imx8mmevk = "3.10.0"
MACHINE_FEATURES_remove_imx8mmevk = "qca9377 qca6174"

# Cross machines / BSPs
## No need to install u-boot, already a WKS dependency
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_remove_imx = "u-boot-fslc"
## OP-TEE is a dependency of u-boot (fit), no need for WKS_FILE_DEPENDS
OPTEE_WKS_FILE_DEPENDS_imx = ""
