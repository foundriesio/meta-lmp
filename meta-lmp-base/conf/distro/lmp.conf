require conf/distro/include/lmp.inc

DISTRO = "lmp"
DISTRO_NAME = "Linux-microPlatform"

IMAGE_LINGUAS ?= "en-us"

# SOTA
OSTREE_OSNAME = "lmp"
INITRAMFS_IMAGE = "initramfs-ostree-lmp-image"
INITRAMFS_FSTYPES = "cpio.gz"
OSTREE_KERNEL ?= "${@oe.utils.conditional('KERNEL_IMAGETYPE', 'fitImage', '${KERNEL_IMAGETYPE}-${INITRAMFS_IMAGE}-${MACHINE}-${KERNEL_FIT_LINK_NAME}', '${KERNEL_IMAGETYPE}', d)}"
OSTREE_KERNEL_ARGS_COMMON ?= "root=LABEL=otaroot rootfstype=ext4"
OSTREE_KERNEL_ARGS ?= "${OSTREE_KERNEL_ARGS_COMMON}"
DISTRO_FEATURES_append = " sota usrmerge"
DISTRO_FEATURES_NATIVE_append = " sota"
INHERIT += "sota reproducible_build"
## No need to install the kernel image into the boot partition
IMAGE_BOOT_FILES_remove = " ${KERNEL_IMAGETYPE}"
## Prefer aktualizr-lite as the default SOTA_CLIENT
SOTA_CLIENT ?= "aktualizr-lite"
## Disable default prov by default to allow runtime configuration
SOTA_CLIENT_PROV ?= ""
## Also produce OTA rootfs tarball for additional image support
BUILD_OTA_TARBALL = "1"
## Support Aktualizr with HSM by default for secure SOTA
SOTA_CLIENT_FEATURES_append = " hsm"
## Sota BSP specific configs are managed by LMP
SOTA_MACHINE_lmp = "lmp"
## Allow custom SOTA_CLIENT providers (e.g. aktualizr-lite)
IMAGE_INSTALL_append = " ${@oe.utils.ifelse('${SOTA_CLIENT}' != 'aktualizr', '${SOTA_CLIENT}', '')}"
IMAGE_INSTALL_remove = "${@oe.utils.ifelse('${SOTA_CLIENT}' != 'aktualizr', 'aktualizr aktualizr-info', '')}"
## Do not ship repo manifest in /etc by default (published by CI instead)
IMAGE_CLASSES_remove = "image_repo_manifest"

# Prelinking increases the size of downloads and causes build errors
USER_CLASSES_remove = "image-prelink"

# For live/installer images
INITRD_IMAGE_LIVE ?= "core-image-minimal-initramfs"
LIVE_ROOTFS_TYPE = "ota-ext4"
LABELS_LIVE = "install"

# General
WKS_FILE_DEPENDS_BOOTLOADERS_remove = "syslinux systemd-boot"
